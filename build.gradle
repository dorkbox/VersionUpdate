/*
 * Copyright 2018 dorkbox, llc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.nio.file.Paths
import java.time.Instant

buildscript {
    // load properties from custom location
    def propsFile = Paths.get("${projectDir}/../../gradle.properties").toAbsolutePath().normalize().toFile()
    println("Loading custom property data from: ${propsFile}")

    def props = new Properties()
    propsFile.withInputStream { props.load(it) }
    props.each { key, val ->
        project.ext.set(key, val)
    }

    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        // for plugin publishing
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.10'

        // for license sources
        classpath 'gradle.plugin.com.dorkbox:Licensing:1.2.2'
        classpath 'gradle.plugin.com.dorkbox:Licensing:1.2.2:sources'
    }
}

plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'

    id 'com.dorkbox.Licensing' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.2.60'
}

// for publishing the plugin to gradle
apply plugin: 'com.gradle.plugin-publish'


project.description = 'Gradle Plugin to update version information and git tags within the Gradle project and java/kotlin files'
project.group = 'com.dorkbox'
project.version = '1.2'

project.ext.name = 'Gradle Version Update and Git Tag Plugin'
project.ext.id = 'VersionUpdate'
project.ext.url = 'https://git.dorkbox.com/dorkbox/VersionUpdate'
project.ext.tags = ['version', 'versioning', 'semver', 'semantic-versioning']

licensing {
    license(License.APACHE_2) {
        author 'dorkbox, llc'
        url project.ext.url
        note 'Plugin to set version information in maven POM, java or kotlin files, and git tags'
        note """Git tag code based upon 'gradle-git-version', Copyright 2015, Palantir Technologies. https://github.com/palantir/gradle-git-version"""
    }
}

sourceSets {
    main {
        java {
            setSrcDirs Collections.singletonList('src')
        }
    }
}

repositories {
    jcenter()
}


dependencies {
    compileOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.60'

    implementation 'org.eclipse.jgit:org.eclipse.jgit:4.5.4+'
    implementation 'com.dorkbox:Version:1.0'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    runtime 'ch.qos.logback:logback-classic:1.1.6'
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
}

tasks.withType(Jar) {
    duplicatesStrategy DuplicatesStrategy.FAIL

    manifest {
        attributes['Implementation-Version'] = project.version
        attributes['Build-Date'] = Instant.now().toString()
    }
}

///////////////////////////////
//////    Plugin Publishing + Release
///////////////////////////////
gradlePlugin {
    plugins {
        Version {
            id = "${project.group}.${project.ext.id}"
            implementationClass = 'dorkbox.version.VersionPlugin'
        }
    }
}

pluginBundle {
    website = project.ext.url
    vcsUrl = project.ext.url

    plugins {
        version {
            id = "${project.group}.${project.ext.id}"
            displayName = project.ext.name
            description = project.description
            tags = project.ext.tags
            version = project.version
        }
    }
}
